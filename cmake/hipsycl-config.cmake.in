cmake_minimum_required(VERSION 3.5.1)

set(HIPSYCL_SYCLCC "@HIPSYCL_INSTALL_LOCATION@/bin/syclcc-clang")
set(HIPSYCL_CPU_BACKEND_AVAILABLE "@WITH_CPU_BACKEND@")
set(HIPSYCL_CUDA_BACKEND_AVAILABLE "@WITH_CUDA_BACKEND@")
set(HIPSYCL_ROCM_BACKEND_AVAILABLE "@WITH_ROCM_BACKEND@")

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
enable_language(SYCL)

include(hipsycl-targets)

function(add_sycl_to_target)
  set(options)
  set(one_value_keywords TARGET)
  set(multi_value_keywords SOURCES)
  cmake_parse_arguments(ADD_SYCL
    "${options}"
    "${one_value_keywords}"
    "${multi_value_keywords}"
    ${ARGN}
  )

  if(HIPSYCL_PLATFORM_CANONICAL STREQUAL "cpu")
    target_link_libraries(${ADD_SYCL_TARGET} PUBLIC hipSYCL::hipSYCL_cpu)
  elseif(HIPSYCL_PLATFORM_CANONICAL MATCHES "cuda")
    target_link_libraries(${ADD_SYCL_TARGET} PUBLIC hipSYCL::hipSYCL_cuda)
  else()
    target_link_libraries(${ADD_SYCL_TARGET} PUBLIC hipSYCL::hipSYCL_rocm)
  endif()

  # For now we have to link using syclcc, as CMake doesn't know about all
  # the required libraries that need to be linked with the target.
  set_target_properties(${ADD_SYCL_TARGET} PROPERTIES LINKER_LANGUAGE SYCL)

  set_source_files_properties(${ADD_SYCL_SOURCES} PROPERTIES LANGUAGE SYCL)
endfunction()

